
 /*=======================================================================*/
 /*                                                                       */
 /*           (c) InstallShield Software Corporation (1996-1997)          */
 /*            (c)  InstallShield Corporation  (1990-1996)                */
 /*                       Schaumburg, Illinois 60173                      */
 /*                          All Rights Reserved                          */
 /*                           InstallShield (R)                           */
 /*                                                                       */
 /*   File    : sdwel.rul                                                 */
 /*                                                                       */
 /*   Purpose : This file contains the code for the SdWelcome             */
 /*             script dialog function.                                   */
 /*                                                                       */
 /*=======================================================================*/

 /*------------------------------------------------------------------------*/
 /*                                                                        */
 /*   Function: SdWelcome                                                  */
 /*                                                                        */
 /*   Descrip:  This dialog will show the welcome message to the user.     */
 /*                                                                        */
 /*   Misc:                                                                */
 /*                                                                        */
 /*------------------------------------------------------------------------*/
function SdWelcome(szTitle, szMsg)
          STRING  szDlg, szTemp;
          NUMBER  nId, nMessage, nTemp;
          HWND    hwndDlg;
          BOOL    bDone;
        begin

          szDlg     = SD_DLG_WELCOME;
          nSdDialog = SD_NDLG_WELCOME;

           // record data produced by this dialog
           if (MODE=SILENTMODE) then
             SdMakeName( szAppKey, szDlg, szTitle, nSdWelcome );
             SilentReadData( szAppKey, "Result", DATA_NUMBER, szTemp, nId );
             return nId;
           endif;


          // ensure general initialization is complete
          if (!bSdInit) then
             SdInit();
          endif;

          if (EzDefineDialog( szDlg, "", "", SD_NDLG_WELCOME ) = DLG_ERR) then
              return -1;
          endif;

          // Loop in dialog until the user selects a standard button
          bDone = FALSE;

          while (!bDone)

             nId = WaitOnDialog( szDlg );

             switch(nId)
             case DLG_INIT:
                  // Put the corresponding Info in the List Field
                  if( szMsg != "" ) then
                      SdSetStatic(szDlg, SD_STA_MSG, szMsg);
                  endif;

                  hwndDlg = CmdGetHwndDlg( szDlg );
                  SdGeneralInit( szDlg, hwndDlg, STYLE_NORMAL, szSdProduct );

                  if(szTitle != "") then
                       SetWindowText(hwndDlg, szTitle);
                  endif;

             case NEXT:
                  nId    = NEXT;
                  bDone  = TRUE;

              case BACK:
                   nId    = BACK;
                   bDone  = TRUE;

             case DLG_ERR:
                  SdError( -1, "SdWelcome" );
                  nId    = -1;
                  bDone  = TRUE;

             case DLG_CLOSE:
                  SdCloseDlg( hwndDlg, nId, bDone );

             default:
                  // check standard handling
                   if (SdIsStdButton( nId ) && SdDoStdButton( nId )) then
                      bDone = TRUE;
                  endif;
             endswitch;

          endwhile;

          EndDialog( szDlg );
          ReleaseDialog( szDlg );

          SdUnInit( );

          // record data produced by this dialog
          SdMakeName( szAppKey, szDlg, szTitle, nSdWelcome );
          SilentWriteData( szAppKey, "Result", DATA_NUMBER, "", nId );

          return nId;

       end;

